#!/usr/bin/perl
our $APP     = 'rmcd';
our $VERSION = 0.1;
# rmcd
# Copyright (C) 2010 Magnus Woldrich <trapd00r@trapd00r.se>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
########################################################################

use strict;
use Carp;
use Data::Dumper;
use Getopt::Long;
use Cwd qw(abs_path getcwd);
use File::Copy;
use Mplayer::NowPlaying qw($np_log np stream_np);
use RPD::URI qw(geturi listchans);


my $dir_to_copy_to = "$ENV{HOME}/ToTransfer";
my $log            = "$ENV{HOME}/.mplayer/mplayerd.log";
my $fifo           = "$ENV{HOME}/.mplayer/mplayerd.fifo";
my $pidfile        = '/tmp/mplayerd.pid';
my $player         = 'mplayer';
my @playopt        = ('-cache', 200, '-quiet', '-identify',
                      '-idle', '-input', "file=$fifo"
                    );
$np_log = $log;


# Check if files ^ exist
fileexist();

our($opt_radio, $opt_info_style) = (undef);
GetOptions(
  'kill'      => \&killkid,
  'play:s{,}' => sub {shift; load(@_)},
  'cmd:s{,}'  => \&cmd,
  'radio:s'   => \&radio,
  'help'      => \&usage,
  'list'      => \&list_radio,
  'info'      => \&info,
  'np'        => sub {$opt_info_style = 1; info()},
  'cp|copy'   => \&cp,
);

my @files = @ARGV; # No opts specified, go on...

if(-e $pidfile) {
  # started, waiting for commands
  if(!@files) {
    #usage();
  }
  load(@files);
}
else {
  play(@files);
}

if($opt_info_style) {
  info();
}

my %allowed_cmds = (
  next    => 'pt_step 1',
  prev    => 'pt_step -1',
  toggle  => 'pause',
  pause   => 'pause',
  stop    => 'stop',
  fs      => 'fullscreen',
);


sub list_radio { print for(listchans()); exit(0); }

sub fileexist {
  if(!-p $fifo) {
    require POSIX;
    POSIX::mkfifo($fifo, 0666) or croak("Cant mkfifo $fifo: $!");
  }
}

sub load {
  my @toload = @_;
  if(!-e $pidfile) { #not started
    play(@toload);
    exit(0);
  }
  print "$_\n" for @toload;
  open(my $fh, '>', $fifo) or croak("Cant open $fifo: $!");
  print $fh "loadfile @toload\n";
  exit(0);
}

sub cp {
  my $file = np('file');
  if(!$file) {
    print "There's no file to copy!\n";
    exit(1);
  }
  if($file =~ m;(?:http|mms)://;) {
    print "You can not copy a stream\n";
    exit(1);
  }
  if(!-e $dir_to_copy_to) {
    if(mkdir($dir_to_copy_to)) {
      print "Created directory '$dir_to_copy_to'\n";
    }
    else {
      print "Could not create directory $dir_to_copy_to: $!\n";
      exit(1);
    }
  }
  if(copy($file, "$dir_to_copy_to/")) {
    print "'$file' -> '$dir_to_copy_to'\n";
    exit(0);
  }
  else {
    print "Copy failed: $!\n";
    exit(1);
  }
  exit 232;
}

sub radio {
  shift;
  my $key = shift;
  my $stream = geturi($key);
  open(my $fh, '>', $fifo) or croak($!);
  print $fh "loadfile $stream\n";
  close($fh);
  sleep(1);
  info();
  exit(0);
}

sub play {
  my @toplay = @_;
  print "$_\n" for @toplay;
  daemonize();
  exec($player, @playopt, @toplay);
}

sub cmd {
  shift; # rid of GetOpt... 
  print my @cmd = @_;
  open(my $fh, '>', $fifo) or croak("Cant open $fifo: $!");
  print $fh @cmd, "\n";
  close($fh);
}

sub daemonize {
  use POSIX 'setsid';
  my $PID = fork();
  exit(0) if($PID); #parent
  exit(1) if(!defined($PID)); # out of resources

  setsid();
  $PID = fork();
  exit(1) if(!defined($PID));

  if($PID) { # parent
    waitpid($PID, 0);
    unlink($pidfile); # remove the lock when child have died
    exit(0);
  }
  elsif($PID == 0) { # child
    open(my $fh, '>', $pidfile) or die("Cant open $pidfile: $!");
    print $fh $$;
    close($fh);
    open(STDOUT, '>', $log);
    open(STDERR, '>', '/dev/null');
    open(STDIN,  '<', '/dev/null');
  }
}

sub killkid {
  open(my $fh, '<', $pidfile) or croak("Could not open $pidfile: $!");
  my $target = <$fh>;
  close($fh);

  if(kill(9, $target)) {
    print "Mplayerd with PID $target terminated\n";
  }
  else {
    print "Could not kill $target: $!";
  }
  exit(0);
}

sub info {
  my @c = undef;
  my($def,$bold,$italic) = ("\033[0m", "\033[1m", "\033[3m");

  if(`tput colors` > 255) {
    my $e = "\033[38;5";
    @c = (
      "$e;148m", "$e;250m", "$e;249m", "$e;248m", "$e;247m",
      "$e;246m", "$e;245m", "$e;244m", "$e;244m", "$e;243m",
      "$e;172m", "$e;178m",
    );
  }
  else {
    for(my $i=0;$i<9;$i++) {
      push(@c, "\033[3$i".'m');
    }
  }

  if(np('file') =~ m;(?:http|mms)://;) { # stream
    my $np = stream_np();

    my($title, $bitrate, $website, $genre) = ($np->{title}, $np->{bitrate},
    $np->{website}, $np->{genre});
    
    

    if($opt_info_style) {
      printf("%s from %s (%s)", $title, $website, $genre);
      exit(0);
    }

    printf("$bold$c[1]%7s$def: $bold$c[0]%.55s$def\n", 'Title', $title);
    printf("$bold$c[2]%7s$def: %.55s\n", 'Bitrate', $bitrate) unless !$bitrate;
    printf("$bold$c[3]%7s$def: %.55s\n", 'Genre', $genre);
    printf("$bold$c[4]%7s$def: %.55s\n", 'Channel', $website);
  }
  else {
    my $artist  = np('artist');
    my $title   = np('title');
    my $album   = np('album');
    my $year    = np('year');
    my $comment = np('comment');
    my $bitrate = np('bitrate');
    my $genre   = np('genre');
    my $file    = np('file');

    $bitrate = $bitrate / 1000;

    if($opt_info_style) {
      printf("%s - %s (%s, %d)", $artist, $title, $genre, $year);
      exit(0);
    }

    printf("$bold$c[1]%7s$def: $bold$c[0]%.55s$def\n", 'Artist', $artist);
    printf("$bold$c[2]%7s$def: $c[11]%.55s$def\n", 'Album', $album);
    printf("$bold$c[3]%7s$def: $c[10]%.55s$def\n", 'Title', $title);
    printf("$bold$c[4]%7s$def: %.55s$def\n", 'Year',  $year);
    printf("$bold$c[5]%7s$def: %.55s$def\n", 'Comment', $comment);
    printf("$bold$c[6]%7s$def: %d kbps$def\n", 'Bitrate', $bitrate);
    printf("$bold$c[7]%7s$def: %.55s$def\n", 'Genre', $genre);
    printf("$bold$c[8]%7s$def: %.55s$def\n", 'File', $file);

  }
}

sub usage {
  print << "EOF";
  $APP $VERSION
  Usage: rmcd [OPTIONS] (FILES/URI)
  
  OPTIONS
    -r,   --radio   play radio station KEY
    -l,   --list    list radio stations
    -p,   --play    play FILE(s)/URI
    -c,   --cmd     send COMMAND to a running process
    -s    --show    show a list of commands that are supported
    -i    --info    show now playing information
    -n    --np      show now playing information on single line
    -k    --kill    kill the running process

EOF

  exit(0);
}
